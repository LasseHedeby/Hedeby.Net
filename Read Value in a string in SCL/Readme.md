# Read value in a string in SCL
**Date:** 2016-02-19

In a project I needed information in a SIMATIC S7-300 PLC from a Data Matrix code containing a string with values separated by semicolon, like this

`Value1;Value2;Value3;Value4`

Because of the different length of each value I could not just read data from a fixed byte position, but had to search for the right position in the string to read from. And after that I didn’t knew the length of the value I had to read.

I came up with this;
```pascal
//================================================================================================
// Hedeby.Net
// (c)Copyright 2015 All Rights Reserved
//------------------------------------------------------------------------------------------------
// file name:        ReadDataMatrix
// created by:       LHH
// library:          -
// system:           Simatic S7-300 V3.3
// version:          Step 7 V5.5
// restrictions:     Cyclic task
// requirements:     -
// functionality:    This function block read the data matrix string from Jumo sensor and returns
//                   the selected strings
//================================================================================================
// Compiler Options
//================================================================================================
{
OverwriteBlocks          := 'y'; // Overwrite the blocks
GenerateReferenceData    := 'y'; // Generate reference data
CreateObjectCode         := 'y'; // Create the object code
OptimizeObjectCode       := 'y'; // Optimize the object code
MonitorArrayLimits       := 'n'; // Monitor the array limits
CreateDebugInfo          := 'n'; // Create the debug info
SetOKFlag                := 'n'; // Set the OK flag
SetMaximumStringLength   := '254' // Maximum string length
}
//================================================================================================
// Change Log:
//------------------------------------------------------------------------------------------------
// Version  Date          Author   Description
//------------------------------------------------------------------------------------------------
// 0.1      2015.09.02    LHH      Created
// 1.0      2015.09.02    LHH      First release after testing
//
//================================================================================================
FUNCTION_BLOCK ReadDataMatrix
 
TITLE = 'Read Data Matrix'
VERSION: '1.0'
AUTHOR: LHH
 
VAR_INPUT
 valueIndex        : INT;
 arrData           : ARRAY[1..520] OF CHAR;
END_VAR
 
VAR_OUTPUT
 valueOutput       : STRING;
END_VAR
 
VAR
 statIndex         : INT;
 statFieldNumber   : INT;
END_VAR
 
 statFieldNumber := 0;
 valueOutput := '';
  
 // Run through array of charactors
 FOR statIndex := 1 TO 520 DO
   // Read value index
   IF (valueIndex = statFieldNumber) AND NOT (arrData[statIndex] = ';') THEN
     valueOutput := CONCAT(IN1:=valueOutput,IN2:=CHAR_TO_STRING(arrData[statIndex]));
   END_IF;
  
   // Find separator
   IF (arrData[statIndex] = ';') THEN
     statFieldNumber := (statFieldNumber + 1);
   END_IF;
  
   // Stop when reached
   IF (valueIndex < statFieldNumber) THEN
     EXIT;
   END_IF;
 END_FOR;
END_FUNCTION_BLOCK
```

The code is searching through the array of charactors to find the separator “;”.  If `valueIndex` is equal to the actual field number the `valueOutput` is generated by adding a single character to the end of the string until the next separator i found.

When the `valueIndex` is reached the search stops to save cycle time.
